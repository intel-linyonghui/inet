//
// This library is free software, you can redistribute it
// and/or modify
// it under  the terms of the GNU Lesser General Public License
// as published by the Free Software Foundation;
// either version 2 of the License, or any later version.
// The library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
// See the GNU Lesser General Public License for more details.
//
package inet.node.mpls;

import inet.common.MessageDispatcher;
import inet.common.lifecycle.NodeStatus;
import inet.linklayer.contract.ILoopbackInterface;
import inet.linklayer.contract.IPppInterface;
import inet.networklayer.common.InterfaceTable;
import inet.networklayer.ipv4.Ipv4NetworkLayer;
import inet.networklayer.mpls.LibTable;
import inet.networklayer.mpls.Mpls;
import inet.networklayer.rsvp_te.Rsvp;
import inet.networklayer.rsvp_te.SimpleClassifier;
import inet.networklayer.ted.LinkStateRouting;
import inet.networklayer.ted.Ted;


//
// An RSVP-TE capable router.
//
// Rsvp occupies the Transport layer; however, it is not a transport protocol
// itself. Rsvp uses transport protocols to route packets. ~Ted is used
// to calculate shortest paths.
//
module RsvpRouter
{
    parameters:
        @networkNode();
        @labels(node,mpls-node);
        @display("i=abstract/router");
        bool hasStatus = default(false);
        int numLoInterfaces = default(1);
        string peers;
        string routerId = default("auto");
        *.forwarding = true;
        *.routingTable.routerId = routerId;
        *.interfaceTableModule = default(absPath(".interfaceTable"));
        *.routingTableModule = default(absPath(".ipv4.routingTable"));
        *.tedModule = default(absPath(".ted"));
        *.rsvpModule = default(absPath(".rsvp"));
        *.libTableModule = default(absPath(".libTable"));
    gates:
        inout pppg[] @labels(PppFrame-conn);
    submodules:
        status: NodeStatus if hasStatus {
            @display("p=100,80;is=s");
        }
        interfaceTable: InterfaceTable {
            parameters:
                @display("p=100,320;is=s");
        }
        ted: Ted {
            parameters:
                @display("p=100,160;is=s");
        }
        linkStateRouting: LinkStateRouting {
            parameters:
                peers = peers;
                @display("p=600,80");
        }
        rsvp: Rsvp {
            parameters:
                peers = peers;
                classifierModule = "^.classifier";
                @display("p=300,80");
        }
        classifier: SimpleClassifier {
            parameters:
                @display("p=100,400;is=s");
        }
        tn: MessageDispatcher {
            parameters:
                @display("p=450,160;b=500,5,,,,1");
        }
        ipv4: Ipv4NetworkLayer {
            parameters:
                @display("p=450,240");
        }
        nm: MessageDispatcher {
            parameters:
                @display("p=450,320;b=500,5,,,,1");
        }
        lo[numLoInterfaces]: <default("LoopbackInterface")> like ILoopbackInterface {
            @display("p=250,560");
        }
        ppp[sizeof(pppg)]: <default("PppInterface")> like IPppInterface {
            parameters:
                @display("p=400,560,row,150;q=l2queue");
        }
        mpls: Mpls {
            parameters:
                classifierModule = "^.classifier";
                @display("p=450,400");
        }
        libTable: LibTable {
            parameters:
                @display("p=100,240;is=s");
        }
        ml: MessageDispatcher {
            parameters:
                @display("p=450,480;b=500,5,,,,1");
        }
    connections allowunconnected:
        linkStateRouting.ipOut --> tn.in++;
        tn.out++ --> linkStateRouting.ipIn;
        ipv4.transportOut --> tn.in++;
        tn.out++ --> ipv4.transportIn;

        rsvp.ipOut --> tn.in++;
        rsvp.ipIn <-- tn.out++;

        ipv4.ifOut --> nm.in++;
        nm.out++ --> ipv4.ifIn;

        for i=0..numLoInterfaces-1 {
            lo[i].upperLayerOut --> nm.in++;
            nm.out++ --> lo[i].upperLayerIn;
        }

        for i=0..sizeof(pppg)-1 {
            pppg[i] <--> ppp[i].phys;

            ppp[i].upperLayerOut --> ml.in++;
            ml.out++ --> ppp[i].upperLayerIn;
        }
        mpls.ifOut --> ml.in++;
        ml.out++ --> mpls.ifIn;
        nm.out++ --> mpls.netwIn;
        mpls.netwOut --> nm.in++;
}

